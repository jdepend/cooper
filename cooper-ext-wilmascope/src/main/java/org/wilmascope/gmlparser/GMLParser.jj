options {
  LOOKAHEAD = 1;
  CHOICE_AMBIGUITY_CHECK = 2;
  OTHER_AMBIGUITY_CHECK = 1;
  STATIC = false;
  DEBUG_PARSER = false;
  DEBUG_LOOKAHEAD = false;
  DEBUG_TOKEN_MANAGER = false;
  ERROR_REPORTING = true;
  JAVA_UNICODE_ESCAPE = false;
  UNICODE_INPUT = false;
  IGNORE_CASE = false;
  USER_TOKEN_MANAGER = false;
  USER_CHAR_STREAM = false;
  BUILD_PARSER = true;
  BUILD_TOKEN_MANAGER = true;
  SANITY_CHECK = true;
  FORCE_LA_CHECK = false;
}

PARSER_BEGIN(GMLParser)
package org.wilmascope.gmlparser;
import java.io.*;

public class GMLParser {
}

PARSER_END(GMLParser)

// Lexical definitions
TOKEN_MGR_DECLS: { static int nestingLevel = 0; }
   
<DEFAULT> SKIP :
{
  " " | "\t" | "\n" | "\r" | "\\"
|"Line" : IN_IGNORABLE
|"LabelGraphics" : IN_IGNORABLE
|"node_style" : IN_IGNORABLE
|"edge_style" : IN_IGNORABLE
|"style" : IN_IGNORABLE
|"edgeAnchor" : IN_IGNORABLE
}

<IN_IGNORABLE> SKIP:
{
  "[" { nestingLevel++; }
| "]" { if (--nestingLevel == 0 ) SwitchTo(DEFAULT); }
}

<IN_IGNORABLE> MORE:
{
  <~[]>
}

TOKEN:
{
  < GRAPH: "graph" >
| < VERSION: "version" >
| < DIRECTED: "directed" >
| < NAME: "name" >
| < NODE: "node" >
| < EDGE: "edge" >
| < ID: "id" >
| < LABEL: "label" >
| < SOURCE: "source" >
| < TARGET: "target" >
| < NUM: ( ["0"-"9"] )+ >
| < DEC: "." <NUM> | <NUM> ( "." <NUM> )?  >
| < CTLSTRING: "\""(~["\""])*"\""> {matchedToken.image = matchedToken.image.substring(1, lengthOfMatch - 1); } 
} 

// end lexical definitions, parser start
void graph(GraphClient g) : {}
{
  <GRAPH> "[" stmtList(g) "]"
  <EOF> 
}

void stmtList(GraphClient g) : {String s;}
{
  {
    System.out.println("Parsing the GML graph...");
  }
  ( stmt(g) )*
}

void stmt(GraphClient g) : { NodeClient n; Token t1, t2, t3, t4, w, h; }
{
  <VERSION> <NUM>
| <DIRECTED> <NUM>
| <NODE> "[" { t1=t2=w=h=null; }
    (( <ID> t1=<NUM> 
    | <LABEL> t2=<CTLSTRING> 
    | "graphics" "[" 
      ( "x" t3=<DEC> | "y" t3=<DEC> | "w" w=<DEC> | "h" h=<DEC> | "type" t3=<CTLSTRING> )* "]"
    )+) {
      float width=0,height=0;
      if(w!=null) width=Float.parseFloat(w.image);
      if(h!=null) height=Float.parseFloat(h.image);
      if(t2==null) {
        System.err.println("WARNING: Node id="+t1.image+" has no label!");
        g.addNode(t1.image,"",width,height); 
      } else {
        g.addNode(t1.image,t2.image,width,height);
      }
    } 
  "]"
| <EDGE> "[" { t3 = null; t4 = null; }
    ( <SOURCE> t1=<NUM> <TARGET> t2=<NUM> ( <LABEL> t3=<CTLSTRING> | "visible" <NUM> )*
      "graphics" "[" ( "arrow" t4=<CTLSTRING> | "capstyle" <CTLSTRING> | "splinesteps" <NUM>)* "]" )
    { if(t3==null) {
        g.addEdge(t1.image, t2.image, t4.image);
      } else {
        g.addEdge(t1.image,t2.image,t3.image,t4.image); 
      }
    }
  "]"
  
}

